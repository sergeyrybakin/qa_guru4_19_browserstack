plugins {
    id 'java-library'
    id 'application'
    id 'io.qameta.allure' version '2.8.1'
}

repositories {
    mavenCentral()
}

def allureVersion = "2.13.8",
    ownerVersion = "1.0.12",
    selenideVersion = "5.20.1",
    junitVersion = "5.7.1",
    aspectjVersion = "1.9.6",
    appiumVersion = "7.3.0",
    restassuredVersion = "4.3.3"

allure {
    version = "$allureVersion"
    autoconfigure = true
    aspectjweaver = true
    configuration = "testImplementation"
    useJUnit5 {
        version = "$allureVersion"
    }
}

dependencies {
    implementation (
            "org.aeonbits.owner:owner:$ownerVersion",
            "org.aspectj:aspectjweaver:$aspectjVersion",
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.appium:java-client:$appiumVersion",
            "io.rest-assured:rest-assured:$restassuredVersion",
            "org.junit.jupiter:junit-jupiter-api:$junitVersion",
            "org.junit.jupiter:junit-jupiter-params:$junitVersion")
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task smoke_tests(type: Test) {
    useJUnitPlatform{
        includeTags 'smoke'
    }
}

task android_tests(type: Test) {
    useJUnitPlatform{
        includeTags 'android'
    }
}

task ios_tests(type: Test) {
    useJUnitPlatform{
        includeTags 'ios'
    }
}